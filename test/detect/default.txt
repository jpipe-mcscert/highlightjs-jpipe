justification peer_buddy {

    conclusion learning is "Learning is more efficient for participants"

    strategy design is "Design the protocol using a peer buddy approach"
    design supports learning

    evidence participants is "Participant pool is large enough"
    participants supports design

    evidence non_bias is "Situation does not require isolation"
    non_bias supports design

}


/*

composition {
    justification design is refine(peer_buddy, recruitment) {
        hook: "peer_buddy:participant"
    }
}

*/

justification empowrd_design {

    conclusion learning is "Learning is more efficient for participants"

    strategy design is "Design the protocol using a peer buddy approach"
    design supports learning

    evidence non_bias is "Situation does not require isolation"
    non_bias supports design


    sub-conclusion participants is "Potential participant pool is large enough"
    participants supports design

    strategy and is "Combining recruitments methods"
    and supports participants

    sub-conclusion c1 is "Only interested individuals are contacted"
    c1 supports and

    strategy calling is "Individual calls to invite for participation"
    calling supports c1

    evidence script is "Call script is available"
    script supports calling

    sub-conclusion reused is "Phone directory can be reused"
    reused supports calling

    strategy check is "Assessing the TOU w.r.t. the intended usage"
    check supports reused

    evidence directory is "Phone directory is provided by a partner centre"
    directory supports check

    evidence tou is "Terms of use (TOU)"
    tou supports check 

    sub-conclusion c2 is "General audience has awareness of the event"
    c2 supports and
    strategy display is "Display posters in strategic places"
    display supports c2
    
    evidence poster is "Approved poster file"
    poster supports display

    sub-conclusion c3 is "Established trust exists with potential on-site participants"
    c3 supports and
    strategy chat is "On-site discussion with potential participants"
    chat supports c3
    evidence talking_points is "Identified talking points"
    talking_points supports chat

    sub-conclusion locations is "List of usable locations is available"
    locations supports chat 
    locations supports display

    strategy combining is "Combining both lists into one single"
    combining supports locations 
    evidence public is "List of public location not requiring authorization"
    public supports combining 
    sub-conclusion authorized is "List of authorized locations is available"
    authorized supports combining
    strategy authorization is "Collecting authorization from stakeholders"
    authorization supports authorized
    evidence places is "List of locations requiring authorization"
    places supports authorization
}